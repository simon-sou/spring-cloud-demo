plugins {
    id 'org.springframework.boot' version '2.3.5.RELEASE' apply false
    id 'io.freefair.lombok' version '3.7.5' apply false
}

def springCloudVersion = 'Hoxton.SR8'
def springBootVersion = '2.3.5.RELEASE'
def JavaLang = JavaVersion.current()

// 定义 version
configure(allprojects) {
    version = '1.0.0.SNAPSHOT'
}

// 定义通用依赖
subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
//    apply plugin: 'io.freefair.lombok'
    apply plugin: 'io.spring.dependency-management'

    targetCompatibility = JavaLang
    sourceCompatibility = JavaLang

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }


    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    compileJava {
        options.incremental = true
    }

}

configure(subprojects.findAll { it.name.endsWith('-app') }) {
    apply plugin: 'org.springframework.boot'

    dependencies {
        compile 'org.springframework.cloud:spring-cloud-starter'
        compile 'org.springframework.boot:spring-boot-starter-actuator'
    }
}